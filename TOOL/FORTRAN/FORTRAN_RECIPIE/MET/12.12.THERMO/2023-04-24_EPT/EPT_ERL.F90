FUNCTION EPT_ERL(T,TD,P)
! THIS FUNCTION RETURNS THE EQUIVALENT POTENTIAL TEMPERATURE EPT
! (KELVIN) FOR A PARCEL OF AIR INITIALLY AT TEMPERATURE T (CELSIUS),
! DEW POINT TD (CELSIUS) AND PRESSURE P (MILLIBARS).

IMPLICIT NONE
REAL,INTENT(IN)::T,TD,P
REAL WMR,TCON
REAL W,TLCL, TK, TL, PT

REAL EPT_ERL

! COMPUTE THE MIXING RATIO (GRAMS OF WATER VAPOR PER KILOGRAM OF
! DRY AIR).
W = WMR(P,TD)

!COMPUTE THE TEMPERATURE (CELSIUS) AT THE LIFTING CONDENSATION LEVEL.
TLCL = TCON(T,TD)

TK = T+273.15
TL = TLCL+273.15
PT = TK*(1000./P)**(0.2854*(1.-0.00028*W))
EPT_ERL = PT*EXP((3.376/TL-0.00254)*W*(1.+0.00081*W))

!  THE FORMULA USED
!   IS EQ.(43) IN BOLTON, DAVID, 1980: "THE COMPUTATION OF EQUIVALENT
!   POTENTIAL TEMPERATURE," MONTHLY WEATHER REVIEW, VOL. 108, NO. 7
!   (JULY), PP. 1046-1053. THE MAXIMUM ERROR IN EPT IN 0.3C.  IN MOST
!   CASES THE ERROR IS LESS THAN 0.1C.

RETURN
END



FUNCTION DWPT(T,RH)
! INPUT
! T: TEMPERATURE(CELSIUS) 
! RH: RELATIVE HUMIDITY (%).
!
! OUTPUT
! DEW POINT (CELSIUS)

IMPLICIT NONE
REAL,INTENT(IN)::T,RH
REAL DWPT
REAL X,DPD

X = 1.-0.01*RH

! COMPUTE DEW POINT DEPRESSION.
DPD =(14.55+0.114*T)*X+((2.5+0.007*T)*X)**3+(15.9+0.117*T)*X**14

DWPT = T-DPD

! THE FORMULA IS USED IN THE
! PROCESSING OF U.S. RAWINSONDE DATA AND IS REFERENCED IN PARRY, H.
! DEAN, 1969: "THE SEMIAUTOMATIC COMPUTATION OF RAWINSONDES,"
! TECHNICAL MEMORANDUM WBTM EDL 10, U.S. DEPARTMENT OF COMMERCE,
! ENVIRONMENTAL SCIENCE SERVICES ADMINISTRATION, WEATHER BUREAU,
! OFFICE OF SYSTEMS DEVELOPMENT, EQUIPMENT DEVELOPMENT LABORATORY,
! SILVER SPRING, MD (OCTOBER), PAGE 9 AND PAGE II-4, LINE 460.
RETURN
END



FUNCTION WMR(P,T)

!   THIS FUNCTION APPROXIMATES THE MIXING RATIO WMR (GRAMS OF WATER
!   VAPOR PER KILOGRAM OF DRY AIR) GIVEN THE PRESSURE P (MB) AND THE
!   TEMPERATURE T (CELSIUS). 

!THE FORMULA USED IS GIVEN ON P. 302 OF THE
!SMITHSONIAN METEOROLOGICAL TABLES BY ROLAND LIST (6TH EDITION).


!   EPS = RATIO OF THE MEAN MOLECULAR WEIGHT OF WATER (18.016 G/MOLE)
!         TO THAT OF DRY AIR (28.966 G/MOLE)

IMPLICIT NONE

REAL,INTENT(IN)::P,T
REAL WMR
REAL EPS, X, WFW, FWESW, ESW, R

DATA EPS/0.62197/

!   THE NEXT TWO LINES CONTAIN A FORMULA BY HERMAN WOBUS FOR THE 
!   CORRECTION FACTOR WFW FOR THE DEPARTURE OF THE MIXTURE OF AIR
!   AND WATER VAPOR FROM THE IDEAL GAS LAW. THE FORMULA FITS VALUES
!   IN TABLE 89, P. 340 OF THE SMITHSONIAN METEOROLOGICAL TABLES,
!   BUT ONLY FOR TEMPERATURES AND PRESSURES NORMALLY ENCOUNTERED IN
!   IN THE ATMOSPHERE.

X = 0.02*(T-12.5+7500./P)
WFW = 1.+4.5E-06*P+1.4E-03*X*X
FWESW = WFW*ESW(T)
R = EPS*FWESW/(P-FWESW)

!   CONVERT R FROM A DIMENSIONLESS RATIO TO GRAMS/KILOGRAM.

WMR = 1000.*R

RETURN
END




FUNCTION ESW(T)

!   THIS FUNCTION RETURNS THE SATURATION VAPOR PRESSURE ESW (MILLIBARS)
!   OVER LIQUID WATER GIVEN THE TEMPERATURE T (CELSIUS). 


!   ES0 = SATURATION VAPOR RESSURE OVER LIQUID WATER AT 0C

IMPLICIT NONE

REAL,INTENT(IN)::T
REAL ES0, POL
REAL ESW

DATA ES0/6.1078/

POL = 0.99999683      + T*(-0.90826951E-02 + &
& T*(0.78736169E-04   + T*(-0.61117958E-06 + &
& T*(0.43884187E-08   + T*(-0.29883885E-10 + &
& T*(0.21874425E-12   + T*(-0.17892321E-14 + &
& T*(0.11112018E-16   + T*(-0.30994571E-19)))))))))

ESW = ES0/POL**8

! THE POLYNOMIAL
!   APPROXIMATION BELOW IS DUE TO HERMAN WOBUS, A MATHEMATICIAN WHO
!   WORKED AT THE NAVY WEATHER RESEARCH FACILITY, NORFOLK, VIRGINIA,
!   BUT WHO IS NOW RETIRED. THE COEFFICIENTS OF THE POLYNOMIAL WERE
!   CHOSEN TO FIT THE VALUES IN TABLE 94 ON PP. 351-353 OF THE SMITH-
!   SONIAN METEOROLOGICAL TABLES BY ROLAND LIST (6TH EDITION). THE
!   APPROXIMATION IS VALID FOR -50 < T < 100C. 

RETURN
END


FUNCTION TCON(T,D)

!  THIS FUNCTION RETURNS THE TEMPERATURE TCON (CELSIUS) AT THE LIFTING
!   CONDENSATION LEVEL, GIVEN THE TEMPERATURE T (CELSIUS) AND THE
!   DEW POINT D (CELSIUS).

IMPLICIT NONE
REAL,INTENT(IN)::T,D
REAL S,DLT
REAL TCON

!   COMPUTE THE DEW POINT DEPRESSION S.
S = T-D

!   THE APPROXIMATION BELOW, A THIRD ORDER POLYNOMIAL IN S AND T,
!   IS DUE TO HERMAN WOBUS. THE SOURCE OF DATA FOR FITTING THE
!   POLYNOMIAL IS UNKNOWN.

DLT = S*(1.2185+1.278E-03*T+ &
&  S*(-2.19E-03+1.173E-05*S-5.2E-06*T))

TCON = T-DLT

RETURN
END


