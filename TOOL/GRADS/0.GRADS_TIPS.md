# GrADS Tips
[[_TOC_]]
## 図を描く

### 等値線 (コンター)

#### 等値線ラベルのフォントを設定する

(例)等値線のラベル：色1、太さ2、大きさ0.2、を設定する    

```shell
'set clopts 1 2 0.2'
```

デフォルト値は色番号=1、太さ=1、大きさ=0.09    



#### 白抜きコンター

カラーシェードと等値線を重ね書きする際に、等値線を見やすくするため、太い白線の上に等値線を重ねる  
```shell
'set gxout contour'
'set clab off' ;#LABEL OFF
'set cthick 10';#THICK CONTOUR
'set ccolor 0' ;#WHITE
'set cint 5'   ;#CONTOUR INTERVAL
'set cmin 260'
'set cmax 300'
'd var'
'set clab on'
'set cthick 1'
'set ccolor 1' ;#BLACK
'set cint 5'
'set cmin 260'
'set cmax 300'
'set clskip 2'
'd  var'
```



### ベクトルを描く

http://www.jamstec.go.jp/frsgc/research/iprc/nona/GrADS/scripts/vec.html
```shell
vec.gs u;v -SCL 0.7 5.0 -P 1.0 2.5 -SL m/s
```
```shell
ga->  vec.gs variable [-SCL size magnitude (same as "set arrscl")]
                               [-P xp yp (arrow label starts at (xp,yp)]
                               [-SL string]
                               [-HDSZ]
```
#### vec.gsのオプション

```
-SCL: set size and magnitude of arrow label
-P  : set position of arrow label (to use -P, "-SCL" need to be sed.)
-SL : set string written at the end of the label
-HDSZ: set size of arrow head of the label (default=0.15)
```

  

#### 白抜きベクトル＋図の右下に凡例

```bash
'set gxout vector'
'set cthick 10'
'set ccolor  0'
'vec.gs skip(u,10);v -SCL 0.5 20 -P 20 20 -SL m/s'
'q gxinfo'
line=sublin(result,3); xl=subwrd(line,4); xr=subwrd(line,6)
line=sublin(result,4); yb=subwrd(line,4); yt=subwrd(line,6)

xx=xr-0.65
yy=yb-0.35
'set cthick  2'
'set ccolor  1'
'vec.gs skip(u,10);v -SCL 0.5 20 -P 'xx' 'yy' -SL m/s'
```



#### ベクトルを間引く(skip)＋一定の大きさ以上のベクトルのみ書く(maskout)

```bash
'vec.gs skip(maskout(u,mag(u,v)-0.15),5);v  -SCL 0.25 1 -P 'xx' 'yy' -SL m/s'
```

0.15以上のベクトルを5本おきにかく  



#### 2種類のベクトルのスケールを使う

黒潮域とその周辺、ジェット気流とその周辺など、流速の大きさが大きく変わる領域で使う  
/work05/manda/20.AGO_WAN/06.JCOPE2M/05-06-02.SNAP_201201-02  

```bash
VSKIP=1; VMAX=0.5
VSCL1="0.5 $VMAX"
VSCL2="0.5 2"

# V < ${VMAX}
'set time 00Z${dd}${mmm}${yyyy}'
'set lev ${LEV}'

'set gxout vector'
'set cthick 10'
'set ccolor  0'

'vecmy.gs skip(maskout(U.3,${VMAX}-mag(U.3,V.4)),${VSKIP});V.4 -SCL ${VSCL1} -P 20 20 -SL m/s'

# GET COORDINATES OF 4 CORNERS
'q gxinfo'
line3=sublin(result,3); line4=sublin(result,4)
xl=subwrd(line3,4); xr=subwrd(line3,6)
yb=subwrd(line4,4); yt=subwrd(line4,6)

xx=xl+1.5
yy=yt-0.6
'set cthick  2'
'set ccolor  1'

'vecmy.gs skip(maskout(U.3,${VMAX}-mag(U.3,V.4)),${VSKIP});V.4 -SCL ${VSCL1} -P 'xx' 'yy' -SL m/s'



# V > ${VMAX}
'set gxout vector'
'set cthick 10'
'set ccolor  0'

'vecmy.gs skip(maskout(U.3,mag(U.3,V.4)-${VMAX}),${VSKIP});V.4 -SCL ${VSCL2} -P 20 20 -SL m/s'

xx=xl+1.5
yy=yt-1
'set cthick  2'
'set ccolor  15'
'vecmy.gs skip(maskout(U.3,mag(U.3,V.4)-${VMAX}),${VSKIP});V.4 -CLR 15 -SCL ${VSCL2} -P 'xx' 'yy' -SL m/s'
```

#### ベクトルの大きさ

```
mag(u, v)
```
mag = uとvの2乗和の平方根  



### スカラー量の勾配 (GRADIENT)

```bash
say 
say GRADIENT
say
'pi=3.14159265359'
'dtr=pi/180'
'a=6.371e6'

'vpt=theta'
'var=smth9(  vpt )'

'dx = a * cos( dtr * lat )* dtr *cdiff( lon , x )'
'dy = a * dtr *cdiff( lat ,  y )'
'gradx=cdiff( var , x )/ dx'
'grady=cdiff( var , y )/ dy'

'grd=mag( gradx , grady )'

'set gxout contour'
'set cthick 3'
'set clab on'
#'set clevs  5 10 15'
'd grd'
```



### 線を引く
#### 地図上に直線を引く

緯度120.0度、経度20.0度から緯度140.0度、経度40.0度に色1、線種1、太さ6の線をプロット  

```bash
'trackplot 120.0 20.0 140.0 40.0 -c 1 -l 1 -t 6'
```
xy座標系でも使える  
```bash
'trackplot 350 1000 350 100 -c 1 -l 1 -t 6 '
```



#### 時系列のグラフに線を引く  

```bash
'q w2xy 01JAN2006 -4'
x1=subwrd(result,3); y1=subwrd(result,6)
'q w2xy 01JAN2006  4'
x2=subwrd(result,3); y2=subwrd(result,6)
say ' 'x1' 'y1' 'x2' 'y2
'set line 1 3 1'
'draw line 'x1' 'y1' 'x2' 'y2
```



#### 箱（矩形，四角形）を書く  

```bash
ALONW=129 ;ALONE=132
ALATS=31.5;ALATN=33.5
```
```bash
'trackplot ${ALONW} ${ALATS} ${ALONE} ${ALATS} -c 1 -l 1 -t 6'
'trackplot ${ALONE} ${ALATS} ${ALONE} ${ALATN} -c 1 -l 1 -t 6'
'trackplot ${ALONE} ${ALATN} ${ALONW} ${ALATN} -c 1 -l 1 -t 6'
'trackplot ${ALONW} ${ALATN} ${ALONW} ${ALATS} -c 1 -l 1 -t 6'
```



### マークを書く

```bash
'markplot 136.8 34 -c 1 -m 2 -s 0.2'
```



#### 白抜きマークを書く

````bash
LON1=128.350278
LAT1=31.992222
MARK1=2
CLR1=2
````

```bash
'set line 0 1 10'
'markplot $LON1 $LAT1 -m ${MARK1} -s ${SIZE} -c 0'
'set line ${CLR1} 1 2'
'markplot $LON1 $LAT1 -m ${MARK1} -s ${SIZE} -c ${CLR1}'
```



#### タイル（ハッチ）を書く

http://kodama.fubuki.info/wiki/wiki.cgi/GrADS/script/tile.gs?lang=jp
```
V1=RH
MIN=95; MAX=100
TYPE=3
```
```
'tile -thickness 1 -color 0 -type $TYPE $V1 $MIN $MAX'
```
タイルの種類
0 - tile image is provided with specified filename  
1 - solid  
2 - dots  
3 - diagonal downward lines  
4 - diagonal upward lines  
5 - diagonal cross hatching  
6 - vertical lines  
7 - horizontal lines  
8 - rectangular cross hatching  



### 折れ線グラフ 

#### 平均値と標準偏差(標準偏差はシェード)

x=平均, XL=平均-標準偏差, XU=平均+標準偏差  

```bash
'set rgb 21 255   0   0 50'
'set rgb 22   0   0 255 50'
'set gxout linefill'
'set lfcols 1 21'
'set grid off'
'd XL;XU'

'set xlab off'
'set ylab off'
'set gxout line'
'set cmark 0'
'set ccolor 2'
'set cthick 10'
'd X'
```



### 図の大きさを指定
#### 縦書き

```shell
xs = 1
ytop=9

xwid = 5
ywid = 6

xe = xs + xwid
ye = ytop
ys = ye - ywid

'set vpage 0.0 8.5 0.0 11'
'set parea 'xs ' 'xe' 'ys' 'ye
```


### パネルに分割

#### 自動分割

```bash
nmax=7
xmax=2
ymax=4

nmap=1
xmap=1
ymap=1

while ( nmap <= nmax )

mod=math_mod(nmap, xmax)

#say 'mod='mod'   nmap='nmap'   xmap='xmap'   ymap='ymap

if ( mod = 0 )
xmap=1
ymap=ymap+1
else
xmap=xmap+1
endif

nmap=nmap+1

endwhile #nmap
'quit'
```



#### 紙を縦に使う

```shell
'set vpage 0.0 8.5 0 10.5'

xmax=2
ymax=1

xleft=1
ytop=9

xwid =  7/xmax
ywid =  9.5/ymax
xmargin=0.5
ymargin=0.5

ymap=1
xmap=1

xs = xleft + (xwid+xmargin)*(xmap-1)
xe = xs + xwid
ye = ytop - (ywid+ymargin)*(ymap-1)
ys = ye - ywid

'set parea 'xs ' 'xe' 'ys' 'ye
```

```bash
'set time $TIME1'
'q dims'
line=sublin(result,5)
t1=subwrd(line,9)
say 't1='t1' $TIME1'

'set time $TIME2'
'q dims'
line=sublin(result,5)
t2=subwrd(line,9)
say 't2='t2' $TIME2'
say 

'cc'
'set vpage 0.0 8.5 0 10.5'

'set grads off'

xmax=2
ymax=6


x0=0.5
y0=10

xsize=8
ysize=9.5

xwid =  xsize/xmax
ywid =  ysize/ymax

xmargin=0.
ymargin=0.

t=t1

xmap=1
while (xmap <= xmax)

ymap=1
while (ymap <= ymax)

'set t 't
'q dims'
line=sublin(result,5)
time=subwrd(line,6)
  tt=subwrd(line,9)

xs = 0.5 + (xwid+xmargin)*(xmap-1)
xe = xs + xwid
ye = ytop - (ywid+ymargin)*(ymap-1)
ys = ye - ywid

say tt' 'time' 'xmap' 'ymap' '\
math_format("%5.1f",xs)' 'math_format("%5.1f",xe)' '\
math_format("%5.1f",ys)' 'math_format("%5.1f",ye)

'set parea 'xs ' 'xe' 'ys' 'ye

'set xlopts 1 2 0.12'
'set ylopts 1 2 0.12'


t=t+1

ymap=ymap+1
endwhile

xmap=xmap+1
endwhile

```



#### 紙を横に使う

```shell
'set vpage 0.0 11 0 8.5'

xmax=2
ymax=6

ywid =  8/ymax
xwid =  8/xmax
xmargin=0.5
ymargin=0.5

ytop=7

xmap=1; ymap=1

xs = 0.5 + (xwid+xmargin)*(xmap-1)
xe = xs + xwid
ye = ytop - (ywid+ymargin)*(ymap-1)
ys = ye - ywid

'set parea 'xs ' 'xe' 'ys' 'ye
```



### カラーバー

#### color.gs  

```shell
LEVS="-3 3 1"
# LEVS=" -levs -3 -2 -1 0 1 2 3"
```
```shell
KIND="grainbow"
```

```shell
'color ${LEVS} -kind ${KIND} -gxout shaded'
```

#### color.gsの色指定

寒色から暖色    

```shell
KIND='midnightblue->deepskyblue->lightcyan->lightseagreen->white->yellow->orange->red->magenta'
```

寒色から暖色

```shell
KIND='cyan->blue->palegreen->azure->white->papayawhip->gold->red->magenta'
```

寒色から暖色  

```shell
KIND='darkblue->skyblue->white->orange->darkred'
```

寒色から暖色  

```shell
KIND='darkblue->deepskyblue->skyblue->white->white->gold->orange->crimson'
```

淡い青から淡い赤 (Takemi 2019 SOLA)  

```shell
KIND='cornflowerblue->oldlace->tomato'
```
黄色から赤  

```shell
KIND='white->lightyellow->wheat->gold->orange->tomato->red->firebrick'
```

WHITE+RAINBOW  

```bash
KIND='white->blue->aqua->lime->yellow->red'
```

白→濃い青→緑→黄色→赤 (レーダー反射強度用)  

```bash
KIND='white->darkblue->green->yellow->orange->darkred'
```

乾色から湿色  

```shell
KIND='maroon->saddlebrown->darkgoldenrod->khaki->white->white->palegreen->lightgreen->limegreen->darkgreen'
```

湿色から乾色  

```shell
KIND='darkgreen->limegreen->lightgreen->palegreen->white->white->khaki->darkgoldenrod->saddlebrown->maroon'
```

一定値以上とそれ以下を強調  

（降水量や水蒸気量の気候値など）
```shell
KIND="white->antiquewhite->mistyrose->lightpink->mediumvioletred->navy->darkblue->blue->dodgerblue->aqua"
```

負の値の割合を少なく  

```shell
KIND='blue->lightcyan->white->lightgreen->yellowgreen->khaki->gold->orange->crimson'
CLEV='-2 8 0.5'
```

赤から白  

````shell
KIND='crimson->red->orange->gold->wheat->white'
````

白から赤  

````shell
KIND='white->wheat->gold->orange->red->crimson'
````

白から黒  

````shell
KIND='white->lightgray->gray->dimgray->black'
````

黒から白  

````shell
KIND='darkslategray->dimgray->lightgray->white'
````

地形
```
'colormaps -l -3 3 0.1 -map terrain1'
```

#### 好みの色調のカラーパレットを作るサイト

http://hclwizard.org:64230/hclwizard/

作成例    

**PinkYL (y -> pink)**    

```
'set rgb 20  253  246  181'
'set rgb 21  251  221  151'
'set rgb 22  249  196  128'
'set rgb 23  246  169  114'
'set rgb 24  242  141  111'
'set rgb 25  235  110  117'
'set rgb 26  226   76  128'
'set ccols 20 21 22 23 24 25 26'
'set clevs 0 20 40 60 80 100'
```



#### 色見本の表示

```bash
# GET COORDINATES OF 4 CORNERS
'q gxinfo'
line3=sublin(result,3); line4=sublin(result,4)
xl=subwrd(line3,4); xr=subwrd(line3,6)
yb=subwrd(line4,4); yt=subwrd(line4,6)
```

横  
```shell
x1=xl; x2=xr
y1=yb-0.5; y2=y1+0.1
'xcbar 'x1' 'x2' 'y1' 'y2 ' -fw 0.1 -fh 0.13 -fs 2 -ft 3 -line on -edge circle'
```
縦  
```shell
x1=xr+0.3; x2=x1+0.1
y1=yb; y2=yt
'xcbar 'x1' 'x2' 'y1' 'y2 ' -fw 0.1 -fh 0.13 -fs 2 -ft 3 -line on -edge circle'
```

横 (色見本と単位)  

```bash
 KIND='white->blue->aqua->lime->yellow->red'
 LEVS='200 550 50'
 FS=2
 UNIT="[g m\`a-2\`n s\`a-1\`n]"                                
```

```bash
'q gxinfo'
line=sublin(result,3); xl=subwrd(line,4); xr=subwrd(line,6)
line=sublin(result,4); yb=subwrd(line,4); yt=subwrd(line,6)

x1=xl; x2=xr-0.5
y1=yb-0.5; y2=y1+0.1
# say x1' 'x2' 'y1' 'y2
'color ${LEVS} -kind ${KIND} -xcbar 'x1' 'x2' 'y1' 'y2' -fs ${FS} -ft 3 -line on'

x=x2+0.5; y=(y1+y2)*0.5
'set string 1 c 3 0'
'set strsiz 0.15 0.18'
'draw string 'x' 'y' ${UNIT}'
```

#### デフォルト色番号

0 white　1 black　2 red　3 green　4dark blue　5 light blue　6 magenta　7 yellow　8 orange　9 purple　10 yellow/green　11 medium blue　12 dark yellow　13 aqua　14 dark purple　15 gray  



#### 透過色

```shell
'set rgb 21   0   0   0  50' ;#GRAY
'set rgb 22   255 0   0 180' ;#RED
'set rgb 22   0 255   0 180' ;#GREEN
'set rgb 23   0   0 255 180' ;#BLUE
```



### 有意でない領域に影を付ける  

例：t値が2以下のところは有意でないので影を付ける  

```
't=abs((SLPAV.2-SLPAV.1)/(SQRT(SLPVR.1+SLPVR.2)/$NE))'
'set gxout shaded'
'set rgb 98 255 255 255   0'
'set rgb 99 0 0 0 50'
'set clevs -100  2 100'
'set ccols 99   99  98'
'd maskout(t,2-t)'
```





### 標準偏差を色で表す

$$
\begin{eqnarray}
\mathrm{avm1sd} & = &  \mathrm{ave} - 1\times \mathrm{sd} \\
\mathrm{avp1sd} & = & \mathrm{ave}  + 1 \times \mathrm{sd} \\
\end{eqnarray}
$$


```shell
'set gxout linefill'
'set lfcols 1 21'
'd avm1sd; avp1sd'
```



### 地図

```shell
set map 色番号 線種番号 太さ
```
```shell
set mpdset lowres/mres/hires
```
```shell
set mpdraw on/off
```

高解像度日本地図  

```bash
'set mpdraw off'
'd T'
'draw shp JPN_adm1'
```



#### 陸/海のマスク

```
 d var
 basemap L/O
```
陸上を色番号1(デフォルトは15)で塗りつぶし、境界の色を色番号2(デフォルトは0)にする  
```
basemap L 色番号1 色番号2
```
basemap.gsでは以下のマスクファイルが必要。wgetでダウンロードできる  
```shell
wget ftp://cola.gmu.edu/grads/scripts/lpoly_lowres.asc
wget ftp://cola.gmu.edu/grads/scripts/lpoly_mres.asc
wget ftp://cola.gmu.edu/grads/scripts/lpoly_hires.asc
wget ftp://cola.gmu.edu/grads/scripts/opoly_lowres.asc
wget ftp://cola.gmu.edu/grads/scripts/opoly_mres.asc
wget ftp://cola.gmu.edu/grads/scripts/opoly_hires.asc
wget ftp://cola.gmu.edu/grads/scripts/lpoly_US.asc
```

set mpdsetで地図の解像度を変更しているときは、低解像度L、中解像度M、高解像度Hを表記する  
```
basemap L/O 色番号1 色番号2 L/M/H
```
basemap.gsは73行目で海陸ascファイルのディレクトリを指定している。使用時はここを適宜書き直すこと。  


#### 陸のマスク (WRF)
```
'd maskout(sst,xland-1.5)'
```


#### 距離の凡例を地図に入れる
```
DX=2       ;# DISTANCE IN km
RLAT=34.3  ;# LATITUDE
LON1=136.6 ;# LONTIGUDE

R=6371     ;#RADIUS OF THE EARTH IN km
PI=3.141592653589793
R2D=57.29577951308

DLON=${DX}/(${R}*math_cos(${RLAT}*${PI}/180))*${R2D}
LON2=LON1+DLON
LAT=${RLAT}

'trackplot ' LON1 ' ' LAT ' ' LON2 ' ' LAT ' -c 1 -l 1 -t 6'
```



##　文字を書く

### 図の外側に文字を書く
#### 図の四隅の座標を取得する

```shell
'q gxinfo'
line=sublin(result,3); xl=subwrd(line,4); xr=subwrd(line,6)
line=sublin(result,4); yb=subwrd(line,4); yt=subwrd(line,6)
```
#### 文字を書く

```shell
x=xl ;# (xl+xr)/2
y=yt+0.2
'set strsiz 0.12 0.15'
'set string 1 c 1 0'
'draw string 'x' 'y'  ${TEXT}'
```

#### 図の中に文字を書く (WORLD座標系を使う)

```
LON=135
LAT=33
TEXT="SOME COMMENTS"
```
```
'q w2xy ${LON} ${LAT}'
xx=subwrd(result,3); yy=subwrd(result,6)
'draw string 'xx+0.1' 'yy+0.1' ${TEXT}'
```
http://www.atmos.rcast.u-tokyo.ac.jp/okajima/topics/grads/grads.html



#### 変数の値を書き出す

```shell
'set time 00Z12JUL2018'
'set lat 32'; 'set lon 130'; 'set lev 1000'
'd var'
value=subwrd(result,4)
say 'VALUE='value
```


#### 書式の指定

```
rc = math_format("%5.1f",num)
```

#### 整数にする

```
rc = math_int(num)
```

#### 実数の表示  

小数点第1位まで表示  
```
pi = 3.141592
a = math_format('%.1f', pi)
say a
```
小数点第1位まで表示（全部で4桁になるように左側に空白を入れる）  
```shell
pi = 3.141592
a = math_format('%4.1f', pi)
say a
```
符号を必ず付けて表示  
```shell
pi = 3.141592
a = math_format('%+10.3f', pi)
say a
```



#### 単位表記

[$\mathrm{^oC}$]  

```
[\`ao\`nC]
```
[$\mathrm{10^{-4}s^{-1}}$]  

```
[10\`a-4\`ns\`a-1\`n]
```



## 軸

### 軸ラベルの指定
色　太さ　大きさ  
```shell
'set xlopts 1 3 0.2'
'set ylopts 1 3 0.2'
```

### 軸にタイトルを入れる
(軸のタイトルは英語のソフトの場合アノテーション (annotation)と称されることがある)  

縦軸のラベル (y軸, y axis)  

```shell
'q gxinfo'
line=sublin(result,3); xl=subwrd(line,4); xr=subwrd(line,6)
line=sublin(result,4); yb=subwrd(line,4); yt=subwrd(line,6)

xx=xl-0.7
yy=(yt+yb)/2
'set strsiz 0.14 0.16'
'set string 1 c 3 90'
'draw string 'xx' 'yy' P [hPa]'
```


### 軸の範囲を指定する

```
'set vrange XMIN XMAX'
'set vrange2 YMIN YMAX'
```



### 軸をずらす

```
'set xlpos -0.9 b' ;# X-AXIS BOTTOM OFFSET -0.9
```


### 左右でスケールの違う縦軸を書く

```
'set ylpos 0 l'
'set vrange ya1 ya2'
'd a'
'set xlab off'

'set ylab on'
'set ylpos 0 r'
'set vrange yb1 yb2'
'd b'
```


### 時間軸ラベルを月だけにする

```
set tlsupp year
```


### 時間軸ラベルに年と月を表示しない

```
set tlsupp month
```



## 欠損域に色を塗る

予め全領域に色を塗っておく  
```
'set gxout grfill'
'set clevs -1000 -998'
'set rgb 99 220 220 220'
'set ccols 0 99 0'
'd const(XI, -999, -u)'
```



## 格子線

線の種類は破線(3)、線の色は灰色(15)、線の太さは1  

```
'set grid on 3 15 1'
```



## ポーラーステレオ座標

北極(南極)を中心として地球を上から見た座標系

```
'set mproj nps'
'set map 1 1 1'

'set grid off'

'set xlopts 1 3 0.18'
'set ylopts 1 3 0.18'

#'set mpdset mres'
'set lon ${LONW} ${LONE}'
'set lat ${LATS} ${LATN}'

'set gxout contour'
'set ccolor 1'
'set cthick 3'
'set cint 0.1'
'set clab on'
'set clskip 3'
'set cstyle 1'
'set clopts 1 3 0.12'

'set frame off'
'set grid on'

'd var'

'set font 0'
'circlon 30'
```



### ポーラーステレオ図法で緯度・経度の範囲を指定

```
LONW=-12; LONW=54; LATS=66; LATN=85.5
```

```
'set mpvals $LONW $LONE $LATS $LATN'
```



## 統計

### 統計量の表示

```shell
'set gxout stat'
```
分散、標準偏差、最大、最小値を計算して、画面上に出力する。ただし、時刻は固定されるので、ある時刻の統計量しか求められない。  



## 補間

### 水平方向の補間

```shell
'define ZINTP=lterp(ORG(time=HHZDDMMYYYY), GRD(time=HHZDDMMMYYYYY), bilin)'
```
ZINTP: 補間されたデータ  
ORG: 補間したいデータ  
GRD: 補間する際に参照する格子情報を含むデータ  
ORGとGRDの時刻は同じにしておく必要がある。  
bilin:　補間方法として双線形補間を選択  



### 異なる日付のデータの補間

PL.MAP.SST_ANOM_INDIAN_OCEAN.sh  
```
DATETIMER=00Z01${MMM}${YYYY}
DATETIMEC=00Z01${MMM}0001

'ITPSST=lterp(SST.1(time=${DATETIMER}),sst.2(time=${DATETIMEC}),bilin)'
'set time ${DATETIMEC}'
'd ITPSST-sst.2'
```



### zinterp　指定高度面に補間（鉛直内挿）

zinterp.gs  
ftp://cola.gmu.edu/grads/scripts/zinterp.gs  

公開されているzinterp.gsは拡張子がgsですが、zinterp.gsfという名前にする必要があります。  zinterp.gsしかない場合、  

    cp (GASCRPで指定しているディレクトリ)/zinterp.gs (〃)/zinterp.gsf 


として下さい。  

高度1,000mの値を表示  
```
 rc=gsfallow("on") ;# GrADSスクリプト関数を使用
 'open var.ctl'
 'set x 1 ???'
 'set y 1 ???'
 'set lev 0 2000'
 # 1,000mを挟む二つのレベルを含むように範囲を設定
 'define a='zinterp(var,lev,1000)　
 # 1,000mの高度に内挿したvarをaと定義
 'set lev 1000'
 # 適当に高さを一つに設定
 'd a'
```
- 必ずlevが増えていく座標でないと使用できません 
- 海洋のようにlevの値が減っていく座標のときは、ctlファイルに書かれたlevの順番を逆にし、options zrevを表記すると計算できます



## 鉛直積分　vint

気圧面がhPa表記のとき。最下層から10hPaまでの鉛直積分  
```
d vint(lev(z=1), var, 10)
```
Pa表記のとき  
```
d vint(lev(z=1), var, 1000)/100
```
- 比湿[kg/kg]をvintすると可降水量[mm=kg/m2]になる（vintは各層の値にdp/g[Pa s2/m = kg/m2]をかけているため）  

- ただし対応するctl(nc)の気圧面がPa表記の場合は、hPa表記に直すか、上記のように100で割る必要がある。100000, 1000はあくまでhPaとして受け取られているので、vintをすると100倍のhPa s2/m をかけてしまうため。kg/m2 にするには100で割る必要がある  

- **まちがい**  

```
d vint(var(z=1)-var(z=1)+1000, var, 10)
```
上の方法では、z=1におけるvarが欠損値の場合、開始気圧も欠損値扱いになってしまい、計算ができない    



## 領域平均の時系列

領域平均の時系列をとるときは，aaveとtloopを組み合わせる（aaveは固定された時刻でしか使えないため）  

```bash
set x 1
set y 1
set t 1 31
d tloop(aave(ts,lon=0,lon=360,lat=-90,lat=90))
```



### 時間方向の移動平均

The tloop function can be used to smooth in time:  
```
set lon -180 0
set lat 40
set lev 500
set t 3 28
d tloop(ave(z,t-2,t+2))
```
In this example, we are plotting a time-longitude cross section, where each time is a 5 time period mean centered at that time.  



## 水平移流

### 格子幅

南北方向の格⼦幅をDLAT度, 東西方向の格子幅をDLON度とする。tempが速度(u,v)で移流されるときの移流項の大きさは下記の式で計算できる。

```
dx=6.37*1E6*2*3.14159*cos(lat*3.14159/180)*${DLON}/360
dx2=2*dx
dy=6.37*1E6*2*3.14159*${DLAT}/360
dy2=2*dy
tadv=‐cdiff(temp,x)*u/dx2
tadv=‐cdiff(temp,y)*v/dy2
```

tempの単位がK, 流速の単位がm/sのとき，移流項の単位は K/s

### 移流の東西方向成分

```
tadv=‐cdiff(temp,x)*u/(dx*2)
```

### 移流の南北方向成分

```
tadv=‐cdiff(temp,y)*v/(dy*2)
```

### 格子の東西方向の長さ (m)

```
dx=6.37*1E6*2*3.1416*cos(lat*3.1416/180)*${DLON}/360
```

### 格子の南北方向の長さ (m)

```
dy=6.37*1E6*2*3.1416*${DLAT}/360
```



### 計算の根拠

地球を球体、半径を6370km、円周率を3.14159としたときの⾚道上の円周の長さ（m）

```
6.37*1E6*2*3.14159
```

格⼦の南北方向の長さがDLAT度の時の格子の南北方向の長さ (m)

```
6.37*1E6*2*3.14159*${DLAT}/360
```

緯度lat度上の円周の長さ (m)

```
6.37*1E6*2*3.14159*cos(lat*3.1416/180)
```

緯度lat度における，東西方向格子幅DLON度の東⻄1格⼦分の長さ (m)

```
6.37*1E6*2*3.14159*cos(lat*3.14159/180)*${DLON}/360
```



## 引数の処理

```
function main(args)

# 1つ目の引数を変数xに代入
x = subwrd(args,1)
# xの内容を画面に表示
say x

# 2つ目の引数を変数yに代入
y = subwrd(args,2)
# yの内容を画面に表示
say y
```
```
function sample(args)

# DEFAULT VALUE

a1='value1'
a2='value2'
quitflag='false'

# DECODE OPTIONS

i = 1
while( 1 )
  arg = subwrd( args, i )
  i = i + 1;
  if( arg = '' ); break; endif

  while( 1 )
    if( arg = '-a1' ); a1 = subwrd(args,i)   ;i=i+1; break; endif
    if( arg = '-a2' ); a2 = subwrd(args,i)   ;i=i+1; break; endif
    if( arg = '-q'); quitflag=true; break; endif
     say 'Syntax error : arg= 'arg
    return
  endwhile
endwhile
```



## MSM (NETCDF)用CTLファイル

```
dtype netcdf
OPTIONS yrev
UNPACK  scale_factor add_offset
```



## バイナリ出力

```shell

# OUTPUT TO BINARY FILE (GRADS_TIPS.md)

'q dims'
line=sublin(result,2); xs=subwrd(line,11); xe=subwrd(line,13)
line=sublin(result,3); ys=subwrd(line,11); ye=subwrd(line,13)
line=sublin(result,5); t=subwrd(line,9)

# fwriteの際にはxとyのsetが必須。q dimsで表示されるxの格子点数は1つ大きい場合がある。 (https://seesaawiki.jp/ykamae_grads-note/)
say 'xs = ' xs '   xe = 'xe
say 'ys = ' ys '   ye = 'ye
say 't  = ' t

'set x 'xs' 'xe-1

'set gxout fwrite'
'set fwrite -be ${BIN_FILE_NAME}' ;# -be=big endian output
'd OUT_DATA' ;#OUTPUT DATA
'disable fwrite'
```



### バイナリ出力したデータをFOTRANで読む

```FORTRAN
character(len=500):: IN

pamameter::im=3600,jm=1800

real var(im,jm)
```
```FORTRAN
INQUIRE(IOLENGTH=nreclen)var
write(*,*)'Record length= ',nreclen
write(*,*)'im*jm*4      = ',im*jm*4
open(11,file=IN1,action="read"&
,form='unformatted',access='direct',recl=nreclen)
read(11,rec=1) var
close(11)
```



## その他

### 所要時間を計測する
```shell
'!date -R'

# YOUR CODE GOES HERE.

'!date -R'
```



### 出力ファイルの情報を出力する

```shell
FIG='FIG.eps'
'gxprint 'FIG
'!ls -lh --time-style=long-iso 'FIG
```



### 図の上にタイトルを追加する

```bash
TITLE1="TITLE1"
TITLE2="TITLE2"
```
```bash
say '### TITLE'
'q gxinfo'
line=sublin(result,3); xl=subwrd(line,4); xr=subwrd(line,6)
line=sublin(result,4); yb=subwrd(line,4); yt=subwrd(line,6)

'set strsiz 0.15 0.18'
'set string 1 l 3 0'
x=(xl+xr)/2; y=yt+0.25
'draw string 'x' 'y' ${TITLE1}'
y = y+0.25
'draw string 'x' 'y' ${TITLE2}'
```



### 図にヘッダーを追加する

```shell
HOST=$(hostname);     CWD=$(pwd)
TIMESTAMP=$(date -R); CMD="$0 $@"
GS=$(basename $0 .sh).GS
```
```shell
'q gxinfo'
#say result
line=sublin(result,3); xl=subwrd(line,4); xr=subwrd(line,6)
line=sublin(result,4); yb=subwrd(line,4); yt=subwrd(line,6)

say '### HEADER'
'set strsiz 0.12 0.14'
'set string 1 l 2 0'
xx = 0.2; yy=yt+0.5
'draw string ' xx ' ' yy ' ${GS}'
yy = yy+0.2
'draw string ' xx ' ' yy ' ${CMD}'
yy = yy+0.2
'draw string ' xx ' ' yy ' ${TIMESTAMP}'
yy = yy+0.2
'draw string ' xx ' ' yy ' ${HOST}'
yy = yy+0.2
'draw string ' xx ' ' yy ' ${CWD}'
```



### 文字をきれいにする

```bash
'set hershey off'
```

注：すこし描画に時間がかかるようになる



### テキストファイルの書き出し

```bash
txtfile=TEXT.txt
rm -vf $txtfile
```

```
say 
say '${txtfile}'
say
text='# $(date -R)'
ret = write ('${txtfile}',text,append)
text='# $(pwd)'
ret = write ('${txtfile}',text,append)
text='# $0 $@'
ret = write ('${txtfile}',text,append)
text='YOUR TEXT'
ret = write ('${txtfile}',text,append)
```

###  数字を月の名前に変換する
```
if(mm='01');mmm='JAN';endif; if(mm='02');mmm='FEB';endif
if(mm='03');mmm='MAR';endif; if(mm='04');mmm='APR';endif
if(mm='05');mmm='MAY';endif; if(mm='06');mmm='JUN';endif
if(mm='07');mmm='JUL';endif; if(mm='08');mmm='AUG';endif
if(mm='09');mmm='SEP';endif; if(mm='10');mmm='OCT';endif
if(mm='11');mmm='NOV';endif; if(mm='12');mmm='DEC';endif
```
### 月の名前を数字に変換する
```
if (mmm='JAN') ;mm=01 ;endif; if (mmm='FEB') ;mm=02 ;endif
if (mmm='MAR') ;mm=03 ;endif; if (mmm='APR') ;mm=04 ;endif
if (mmm='MAY') ;mm=05 ;endif; if (mmm='JUN') ;mm=06 ;endif
if (mmm='JUL') ;mm=07 ;endif; if (mmm='AUG') ;mm=08 ;endif
if (mmm='SEP') ;mm=09 ;endif; if (mmm='OCT') ;mm=10 ;endif
if (mmm='NOV') ;mm=11 ;endif; if (mmm='DEC') ;mm=12 ;endif
```



## 関数

### 小数を最も近い整数に直す

```
ret = math_nint(num)
```

### 数値を整形する

```
ret = math_format(format, num)
```

| format | C言語風の書式指定(e.g., %5.2f, %+10.5e) |
| ------ | --------------------------------------- |
| num    | 整形する数値                            |
| ret    | 整形された数値                          |



## テキストファイルの読み込み

```
ret = read(filename)
```

retには2行の情報が返される。1行目がファイル状態、2行目にファイルの内容である  
 ファイル状態は「0」で正常、「1」でエラー、「2」でファイルの最後まで来ています、などとなる。
 一度のreadで1行分の情報しか読み取らないために、複数の行を読み取りたい場合は、複数回実行する必要がある  

### サンプル  

test.dat  
```
this data is test.dat
1.0 2.0 3.0
4.0 5.0 6.0
```

​    

### 読み込み用のスクリプト    

```
ret = read('test.dat')
a = sublin(ret,1)
b = sublin(ret,2)
say 'a is 'a
say 'b is 'b

ret = read('test.dat')
a = sublin(ret,1)
b = sublin(ret,2)
say 'a is 'a
say 'b is 'b

ret = read('test.dat')
a = sublin(ret,1)
b = sublin(ret,2)
say 'a is 'a
say 'b is 'b

'quit'
```

### 結果  

```
a is 0
b is this data is test.dat
a is 0
b is 1.0 2.0 3.0
a is 0
b is 4.0 5.0 6.0
GX Package Terminated 
```

  

## テキストファイルの書き込み

```
ret = write( filename, record )           ;#<- 上書き
ret = write( filename, record, append )   ;#<- 追記
```

appendを指定しない場合、最初のwriteで元のファイルが消えてしまうので注意が必要です。書き込みに成功すると0、失敗すると1又は8が返されます  



### ファイルのクローズ  

```bash
ret = close( filename )
```



## 平均の順序

### 時間平均と領域平均 [![img](https://static.seesaawiki.jp/img/usr_second/common/icon_pen.gif)](https://seesaawiki.jp/ykamae_grads-note/e/edit?id=755469&part=content_1_2)

時間平均と領域平均を行う際は、**時間平均が先**  
```bash
a=ave(var, t=1, t=10)
d aave(a, lon=A, lon=B, lat=C, lat=D)
```
注意: 平面2次元データの場合のみ有効。3次元データの場合, aaveでエラーとなる  

> Error from AAVE  Invalid environment.  Z, T, or E can't vary. 



3次元データを時間・空間平均して、鉛直プロファイルを得る  

```
'set time $TIME1 $TIME2'
'set lev  $ZLEV1 $ZLEV2'

'set lon ${LONW} ${LONE}'
'set lat ${LATS} ${LATN}'

'ave1=aave(VAR,lon=${LONW},lon=${LONE},lat=${LATS},lat=${LATN})'
'd ave(ave1,time=${TIME1},time=${TIME2})'
```
注意: これで合っているかどうか完全に確認とれていない (2020/09/30)  



## 差分の計算

時間方向の前進差分

```
'DA=A-A(t-1)'
```

Aという量の時刻tにおける値からt-1における値を引いた値がDAに代入される



## デフォルトの色設定

0	white
1	black
2	red
3	green
4	dark blue
5	light blue
6	magenta	
7	yellow	
8	orange	
9	purple	
10	yellow/green
11	medium blue
12	dark yellow
13	aqua
14	dark purple
15	gray



## 情報源

- GrADS講座  
  http://akyura.sakura.ne.jp/study/GrADS/kouza/grads.html  
  
- はじめてのGrADS  
  https://sites.google.com/site/afcgrads/  
  
- 東北大学大学院理学研究科 流体地球物理学講座 公開情報/GrADS  
  http://wind.geophys.tohoku.ac.jp/index.php?%B8%F8%B3%AB%BE%F0%CA%F3/GrADS  
  
- GrADS-Note  
  http://seesaawiki.jp/ykamae_grads-note/  
  
- Bin Guan's GrADS Script Library  
  http://bguan.bol.ucla.edu/bGASL.html  
  
- スクリプト集  
  http://kodama.fubuki.info/wiki/wiki.cgi/GrADS/script?lang=jp  
  
- GrADS コマンドリファレンス  
  http://akyura.sakura.ne.jp/study/GrADS/Command/  
  
- IT memo  
  http://hydro.iis.u-tokyo.ac.jp/~kei/?IT%20memo%2FGrADS%20memo  

- GrADS リファレンスマニュアル  
  http://mausam.hyarc.nagoya-u.ac.jp/%7Ehatsuki/grads/gradsmanu.html  

- GrADS Functions (chapter 10)  
  https://www.lmd.jussieu.fr/~lmdz/grads/ga10.htm  

- GrADS Documentation  
  http://cola.gmu.edu/grads/gadoc/gadoc.php  
  
  
