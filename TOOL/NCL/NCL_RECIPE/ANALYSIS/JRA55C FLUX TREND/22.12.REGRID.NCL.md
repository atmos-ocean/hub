# 22.12.REGRID.NCL

/work03/am/2022.SST.TREND.ENERGY.BUDGET/22.22.JRA55C.COARSE.MON/22.12.REGRID.NCL

```
; 
; 02.REGRID.ncl
; 
; Thu, 27 Oct 2022 20:28:49 +0900
; p5820.bio.mie-u.ac.jp
; /work03/am/2022.SST.TREND.ENERGY.BUDGET/32.12.ERA5.COARSE.MON/22.12.REGRID.NCL
;
script_name  = get_script_name()
; スクリプトの名称を取得

print("script="+script_name)
script=systemfunc("basename "+script_name+ " .ncl")
; スクリプトの名前から,拡張子の.nclを削除

NOW=systemfunc("date -R")
; Linuxのdateコマンドを利用して現在時刻を取得する。
; その結果をNOWという変数に代入
HOST=systemfunc("hostname")
CWD=systemfunc("pwd")
; Linuxのpwdコマンド (print working directory)を利用して
; 現在自分がいるディレクトリ名を取得

DSET  = getenv("NCL_ARG_2")
; スクリプトの2番目の引数の内容をDSETという変数に代入
VAR   = getenv("NCL_ARG_3")
; スクリプトの3番目の引数の内容をVARという変数に代入

;MM    = getenv("NCL_ARG_4")
;M=toint(MM)

ys=1980
; 最初のデータの年
ye=2012
; 最後のデータの年

if (VAR .eq. "ULR" .or. VAR .eq. "USR" .or. \
VAR .eq. "DLR" .or. VAR .eq. "DSR" .or. VAR .eq. "SHF" \
.or. VAR .eq. "LHF" )then
TYP="FCT"
elseif (VAR .eq. "TSK" )then
TYP="DIA"
end if
; VARの中身に応じて変数TYPの内容を決める。
; FCT=FORECAST, DIA=DIAGONSTIC

INDIR="/work01/DATA/"+DSET+"/MON/SFC/"+TYP+"/"
; INDIR=入力ファイルのあるディレクトリ(入力ディレクトリ)の名前を指定

ODIR="OUT_NC_SMOOTH_"+DSET
; ODIR: 出力ファイルを保存するディレクトリ(出力ディレクトリ)を指定する
system("mkdir -vp "+ODIR)
; LINUXのmkdirコマンドを使用して出力ディレクトリを作成する。

if (VAR .eq. "ULR")then
VARIN="ulwrf"
elseif (VAR .eq. "USR")then 
VARIN="uswrf"
elseif (VAR .eq. "DLR")then 
VARIN="dlwrf"
elseif (VAR .eq. "DSR")then 
VARIN="dswrf"
elseif (VAR .eq. "SHF")then 
VARIN="shtfl"
elseif (VAR .eq. "LHF")then 
VARIN="lhtfl"
elseif (VAR .eq. "TSK")then 
VARIN="brtmp"
else
print("ERROR in VAR: VAR="+VAR)
end if
; 変数VARの内容に合わせて, 変数VARINの内容を指定している。

INLIST=systemfunc("ls "+INDIR+"*"+VARIN+"*")
; Linuxコマンドのlsコマンドを用いて, INDIRに存在するVARINを含む名称の
; ファイルの一覧を取得

a=addfiles(INLIST,"r")
; 取得したファイル名一覧に含まれる複数のファイルをすべて同時に開く
; aを開いたファイルのポインターとする

;print(a[0])

time=a[:]->initial_time0_hours(:)
; 入力ファイルに含まれるinitial_time0_hoursという変数の内容を, 
; timeという変数に代入する

utc_date = ut_calendar(time, 0)
; timeは特定の日付からの経過時間が数値として指定されているので,
; それを年，月, 日，時間に分ける

year = tointeger(utc_date(:,0))  
month= tointeger(utc_date(:,1)) 
day  = tointeger(utc_date(:,2))
hour = tointeger(utc_date(:,3))
; tointegerは変数の型を整数型に変換している

date_str=sprinti("%0.4i",year)+" "+sprinti("%0.2i",month)\
+" "+sprinti("%0.2i", day)+" "+sprinti("%0.2i", hour)
; 取り出した日付のデータを文字型に変換した後, 全部繋げてdate_str
; に代入している

if (VAR .eq. "TSK")then
time_out=time+6
else
time_out=time
end if
; TSKとその他の変数で時刻の定義が6時間ずれているので, 合わせている

time_out@long_name="time"
; 変数time_outの属性の一つであるlong_nameをtimeに設定している
; 属性: 変数の値以外の変数に関する情報

time_out@units=time@units
; 変数time_outの単位に関する属性(情報)をtimeの属性と同じにする

utc_date_out = ut_calendar(time_out, 0)
year = tointeger(utc_date_out(:,0))  
month= tointeger(utc_date_out(:,1)) 
day  = tointeger(utc_date_out(:,2))
hour  = tointeger(utc_date_out(:,3))

date_str_out=sprinti("%0.4i",year)+" "+sprinti("%0.2i",month)\
+" "+sprinti("%0.2i", day)+" "+sprinti("%0.2i", hour)

print(VAR+" date_str: "+date_str+" date_str_out: "+date_str_out)



lat=a[0]->g0_lat_1
;入力ファイルに含まれる緯度のデータg0_lat_1を変数latに代入する。
lon=a[0]->g0_lon_2
;入力ファイルに含まれる経度のデータg0_lon_2を変数lonに代入する。

;printVarSummary(lat)

print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
print("READ IN "+VAR)

if(VAR .eq. "DLR")then
xin=a[:]->DLWRF_GDS0_SFC_S130(:,:,:)
end if
; 変数VARの中身がDLRであったら, 入力ファイルに含まれるDLWRF_GDS0_SFC_S130
; という名前のデータを変数xinに代入する

if(VAR .eq. "DSR")then
xin=a[:]->DSWRF_GDS0_SFC_S130(:,:,:)
end if

if(VAR .eq. "ULR")then
xin=a[:]->ULWRF_GDS0_SFC_S130(:,:,:)
end if

if(VAR .eq. "USR")then
xin=a[:]->USWRF_GDS0_SFC_S130(:,:,:)
end if

if(VAR .eq. "LHF")then
xin=a[:]->LHTFL_GDS0_SFC_S130(:,:,:)
end if

if(VAR .eq. "SHF")then
xin=a[:]->SHTFL_GDS0_SFC_S130(:,:,:)
end if

if(VAR .eq. "TSK")then
xin=a[:]->BRTMP_GDS0_SFC_S113(:,:,:)
end if


;printVarSummary(xin)
print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
print("")

print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
print("INTERPOLATE TO NEW GRID")
print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
;データの格子間隔の変更
newlat = fspan(-90.,90,180)
; 新しい格子の緯度を-90度から90度まで, 
; 分割数180で用意する(1つの格子の間隔=1度)
newlon = fspan(0.,359.,360)
newlat@units = "degrees_north"
newlon@units = "degrees_east"

lat=lat(::-1)
;元のデータが北極から南に向かう方向にならんでいるので, 
;南極から北に向かう方向に並び替える
xin=xin(:,::-1,:)

xout = linint2_Wrap(lon,lat,xin,True,newlon,newlat,0)
; 新しい格子上にデータを内挿する

xout!0 = "initial_time0_hours"
xout!1 = "g0_lat_1"
xout!2 = "g0_lon_2"
xout&initial_time0_hours=time_out
xout&g0_lat_1 = newlat
xout&g0_lon_2 = newlon



print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
print("OUTPUT NC FILE")
print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
NC1=ODIR+"/"+DSET+".MON.SFC."+VAR+".nc"
; 出力先のファイル名の設定

system("rm -vf "+NC1)
; もし同じ名前のファイルが既に存在していたら, linuxコマンドの
; rmコマンドで削除しておく

OF1 = addfile(NC1,"c")
; 出力ファイルを開く。ファイルのポインターをOF1とする。

if(VAR .eq. "DLR")then
OF1->DLR = xout
end if
; 変数VARの中身がDLRの場合, 出力ファイルに変数xoutの内容を書き出す。
; 出力ファイルの中でのデータの名称はDLRとする。

if(VAR .eq. "DSR")then
OF1->DSR = xout
end if

if(VAR .eq. "ULR")then
OF1->ULR = xout
end if

if(VAR .eq. "USR")then
OF1->USR = xout
end if

if(VAR .eq. "LHF")then
OF1->LHF = xout
end if

if(VAR .eq. "SHF")then
OF1->SHF = xout
end if

if(VAR .eq. "SST")then
OF1->SST = xout
end if

if(VAR .eq. "TSK")then
OF1->TSK = xout
end if



print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
print("PLOT SAMPLE FIGURE")
print("mmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmmm")
;---------------------------------------------
; start graphics
;---------------------------------------------

wks = gsn_open_wks("png","CHK."+DSET+"."+VAR)   

res                     = True        
res@cnFillOn            = True         
res@cnFillPalette       = "gui_default" 
res@cnLinesOn           = False        
res@cnLineLabelsOn      = False         

res@mpMaxLatF           = 60             
res@mpMinLatF           = -60 

res@gsnDraw             = False           
res@gsnFrame            = False            

;res@cnLevelSelectionMode =  "ManualLevels"  
;res@cnMinLevelValF       = -200            
;res@cnMaxLevelValF       = 200            
;res@cnLevelSpacingF      =  50            

res@tmXBLabelFontHeightF     = 0.014         
res@tmYLLabelFontHeightF     = 0.014 
res@tiMainFontHeightF        = 0.022
res@txFontHeightF            = 0.017
res@lbLabelBarOn             = False

;---------------------------------------------
; create plots
;---------------------------------------------
plot = new(2,graphic)
res@tiMainString        = "Original Grid"
res@gsnLeftString=VAR
res@gsnCenterString=""
res@gsnRightString=date_str_out(0)

plot(0) = gsn_csm_contour_map(wks,xin(0,:,:),res)
res@tiMainString        = "Grid after linint2"
plot(1) = gsn_csm_contour_map(wks,xout(0,:,:),res) 

;---------------------------------------------
; create panel
;---------------------------------------------
pres                  = True
pres@gsnPanelLabelBar = True   
gsn_panel(wks,plot,(/2,1/),pres)

print("")
print("Done " + script_name)
print("")

```

